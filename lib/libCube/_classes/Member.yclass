interface LibCube:Member extends LibCube:PrintableValuesObject
    field dimension
        --> domains LibCube:Dimension
        --> documentation "The dimension to which the Member belongs"
        --> xmlMapping XmlMappingProperty_SAVE
        --> dsl -> Textualization
            --> language LANG_en
            --> root "dimension"
            --> number SINGULAR
        ;
    field label
        --> domains Text
        --> documentation "The label of the member (i.e. what the member is called).  This field is the default text written by the toStringInStream function"
        --> xmlMapping XmlMappingProperty_SAVE
        --> dsl -> Textualization
            --> language LANG_en
            --> root "label"
            --> number SINGULAR
        ;	
    field toStringInStreamObject
        --> domains Object
        --> documentation "The toStringInStreamObject of the member (i.e. how the member should be written).  This field is the default text written by the toStringInStream function"
        --> xmlMapping XmlMappingProperty_SAVE
        --> dsl -> Textualization
            --> language LANG_en
            --> root "label"
            --> number SINGULAR
        ;

    field mdxName
        --> domains String
        --> xmlMapping XmlMappingProperty_SAVE
        --> dsl -> Textualization
            --> language LANG_en
            --> root "mdxName"
            --> number SINGULAR
        ;
    
    field mdxFullName
        --> domains String
        --> xmlMapping XmlMappingProperty_SAVE
        --> dsl -> Textualization
            --> language LANG_en
            --> root "mdxFullName"
            --> number SINGULAR
        ;

    method getMdxFullName
    args {
			LibCube:Hierarchy 		hierarchy
			--> nullable			    true
	}
    --> domains String	        

    method generateHierarchyByMdxname(Text previousMember {__nullable},LibCube:Hierarchy child {__nullable})
    --> domains Void

    method buildNodeByMdxName(Text mdxNameToSearch)
    --> domains LibCube:Hierarchy

    method getMemberByMdxName(Text mdxNameToSearch)
    --> domains LibCube:Member

    classProperties
    --> dsl -> Textualization
        --> language LANG_en
        --> root "Cube Member"
        --> number SINGULAR
    ;
    --> trackInstances true
    --> dynamicInstances  true 	
;

implementation LibCube:Member override {
  toStringInStream function
  printValues function
  compareSameClass         
}
  @if (exists_object(Class::areXmlInstancesMergeable) == true)
    --> areXmlInstancesMergeable	true
  @endif

  @if (exists_object(Class::mayModifyClassOfSimilarInstances) == true)
    --> mayModifyClassOfSimilarInstances	true
  @endif         
;

function LibCube:Member::toStringInStream({[_KEY]: Symbol mode {__nullable},} args)
static {
   NlgRepresentant    REPRESENTANT_THE_MEMBER
       --> referential \(
                          \setTextGranuleGender(MASCULINE) 
                          \value(label) 
                       \)
       --> anaphor \(
                      \setTextGranuleGender(MASCULINE) 
                      \value(label) 
                   \)
   ;
}
--> action
    {
        if(this.toStringInStreamObject == null){
            REPRESENTANT_THE_MEMBER.write (_REFERENT: this, args);
        }else{

            \(
            \value(this.toStringInStreamObject, args)
            \).write()
           
        }
        
    } 
;

function LibCube:Member::compareSameClass (LibCube:Member ob)
--> action {

  if(this.mdxName != null && ob.mdxName != null && this.mdxName == ob.mdxName){
    return 0;
  }

} 
;

function LibCube:Member::getMdxFullName
args {
        LibCube:Hierarchy 		hierarchy
        --> nullable			    true
}
--> action {
    if(mdxFullName != null){
        return mdxFullName;
    }

    if(mdxName == null){
        logWarning("Can't get full mdx name of the member: ", this.identifier, " since the mdxName is null");
        return null;
    }

    if(dimension.mdxName == null){
        logWarning("MdxName of the dimension: ", dimension.identifier, "is null");
        return null;
    }
    
    else{
        if(dimension == DIMENSION_TIME){
        	
            //return concat("[", dimension.mdxName, ".DATE].[", mdxName, "]");
            return concat("[", dimension.mdxName, "].", mdxName, "");
            
            //return mdxName;
        }
        else{
            if(hierarchy == null){
            	
                //return concat("[", dimension.mdxName, "].[", mdxName , "]");
                return concat("[", dimension.mdxName, "].", mdxName , "");
            }
            else{
                if(hierarchy.mdxName == null){
                    logWarning("MdxName of the hierarchy: ", hierarchy.identifier, "is null");
                    return null;
                }
                else{
                
                   // return concat("[", dimension.mdxName, ".", hierarchy.mdxName , "].[", mdxName , "]" );
                   return concat("[", dimension.mdxName, ".", hierarchy.mdxName , "].", mdxName , "" );
                }
            }
        }
        
    }
}
;

//Recursive Function to create a hierarchy based on a given MdxName
function LibCube:Member::generateHierarchyByMdxname(Text theMdxName {__nullable},LibCube:Hierarchy child {__nullable})
--> local Text mdxNameAuxiliar,
          Integer indexOfLastOccurrence,
          LibCube:Hierarchy theHierarchy

--> action {
    mdxNameAuxiliar = theMdxName;
    
    //It finds the index of last occurrence of ´].[´
    indexOfLastOccurrence = mdxNameAuxiliar.length() - 
        (ifExpr(mdxNameAuxiliar.reverse().indexOfText(_REGEXP, "[[][\.[]]") == 0) 
            then mdxNameAuxiliar.length() 
            else mdxNameAuxiliar.reverse().indexOfText(_REGEXP, "[[][\.[]]"))
    ;
    //this line to know just if it should create root or child
    //We should consider a flag here to validate if root should be created with specific conditions
    if(indexOfLastOccurrence > 0){
        //Building node based on resulting mdxName, if the node exists,it is returned
        theHierarchy = buildNodeByMdxName(mdxNameAuxiliar);
        mdxNameAuxiliar = mdxNameAuxiliar.truncate(indexOfLastOccurrence - 1);
        if(child != null){
            //Il n’est pas nécessaire d’indiquer l’arbre d’appartenance du nœud puisqu’un nœud appartient à un seul arbre.
            child.removeNode();
            theHierarchy.addChild(child);
        }    
        generateHierarchyByMdxname(mdxNameAuxiliar,theHierarchy);
    }
    else{
        //This part is based on specific client cube configuration (MONDRIAN)
        mdxNameAuxiliar = mdxNameAuxiliar.replace("[", "[All ");
        mdxNameAuxiliar = mdxNameAuxiliar.replace("]", "s]");
        //-------------------------------------------------------------------
        theHierarchy = buildNodeByMdxName(mdxNameAuxiliar);    
        if(child != null){
            //Il n’est pas nécessaire d’indiquer l’arbre d’appartenance du nœud puisqu’un nœud appartient à un seul arbre.
            child.removeNode();
            theHierarchy.addChild(child);
        }
        this.dimension.hierarchies.add(theHierarchy);
    }
    
}
;

function LibCube:Member::buildNodeByMdxName(Text mdxNameToSearch)
--> local 
          LibCube:Hierarchy theHierarchy,
          Collection theNodeCollection
--> action {
    //Search a tree with the same conditions
    theNodeCollection = as(?hierarchy, ?hierarchy.hierarchyIdentifier != null, ?hierarchy.dimension == dimension
    , ?hierarchy.mdxName == mdxNameToSearch );
    if(theNodeCollection.toList().get(_FIRST) != null){
        theHierarchy = theNodeCollection.toList().get(_FIRST);
        if(theNodeCollection.toList().size() > 1){
            logWarning("there is more than one hierarchy with dimension.mdxName:", dimension.mdxName, " and hierarchy.mdxName: " , theHierarchy.mdxName);
        }
    }
    else{
        //If the result of the search is null and the flag parameter is true then the tree is created
        theHierarchy = new(LibCube:Hierarchy);
        theHierarchy.mdxName = mdxNameToSearch;
        theHierarchy.dimension = dimension;
        theHierarchy.hierarchyIdentifier = "unknown";
        theHierarchy.value = getMemberByMdxName(mdxNameToSearch);
    }

    return theHierarchy;
}
;

function LibCube:Member::getMemberByMdxName(Text mdxNameToSearch)
--> local Collection theMemberCollection,
          LibCube:Member theMember
--> action{
    if(mdxName == mdxNameToSearch){
        return this;
    }
    else{
        theMemberCollection = as(?member, ?member.mdxName == mdxNameToSearch, ?member.dimension == dimension
        , ?member.class == LibCube:Member); //waht happen if it is a subclass
        if(theMemberCollection.toList().get(_FIRST) != null){
            theMember = theMemberCollection.toList().get(_FIRST);
            if(theMemberCollection.toList().size() > 1){
                logWarning("there is more than one member with dimension.mdxName:", dimension.mdxName, " and member.mdxName: " , theMember.mdxName);
            }
        }
        else{
            logWarning("there is not a member with dimension.mdxName:", dimension.mdxName, " and member.mdxName: " , mdxNameToSearch);
        }

        return theMember;
    }
}
;



textMethod LibCube:Member::printValues()
--> text 
\( 
    \if(label != null)
        Label: \value(label) Dimension: \value(dimension.identifier)
    \else
        Id: \value(identifier) Dimension: \value(dimension.identifier)
    \endIf
\)
;

